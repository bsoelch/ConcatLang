lib/lists :

core #namespace
stack #include
codeblocks #include

## TODO binary search

reverse public proc( A <?> A mut list  => ){
  toReverse =::
  0 i =::
  toReverse .length 1 - int maxId =:
  while{ i toReverse .length 2 / < do
    toReverse i [] A tmp =:
    toReverse maxId i - []  toReverse i []=
    tmp  toReverse maxId i - []=
    i 1 + i =
  }
}

mergesort_helper proc( A <?> A mut list #+off+# int #+to+# int #+buffer+# A mut list => ){
  buffer =::
  int to =:
  int off mut =:
  input =::
  off to < if{
    off to + 2 / int c mut =:
    input off c buffer mergesort_helper
    c 1 + c =
    input c  to buffer mergesort_helper
    ## 0 ... c-1 and c...to-1 are sorted
    c int i mut =:
    while{ off c < &&{ c to <= } do
      while{ off c < &&{ input off [] input c [] <= } do
        off 1 + off =
      }
      off c < if{
        while{ i to <= &&{ input off [] input i [] <= ! } do
          i 1 + i =
        }
        i c - int delta =:
        delta c off - <= if{
          buffer clear
          buffer input c i [:] <<* drop ## store data in buffer
          input off c [:]  input off delta + c delta + [:]= ## move remaining part of left half to the right
          buffer  input off  off delta + [:]= ## get the elements from buffer
          buffer clear
        else
          buffer clear
          buffer input off c [:] <<* drop ## store data in buffer
          input c i [:]  input off  off delta + [:]= ## move remaining part of left half to the right
          buffer  input off delta + i [:]= ## get the elements from buffer
          buffer clear
        }
        i c =
        off 1 + off =
      }
    }
  }
}

#+ sorts the given list
  elements are compared using 'core .<='
  the sorted list overwrites the given list
 +#
mergesort public proc( A <?> A mut list => ){
  input =::
  #+list+# input #+off+#0 #+to+# input .length 1 - #+buffer+# input .length 2 / A mut list new mergesort_helper
}

mergesort2_helper proc( A <?> A mut list #+cmp+# ( A A => int ) #+off+# int #+to+# int #+buffer+# A mut list => ){
  buffer =::
  int to =:
  int off mut =:
  ( A A => int ) cmp =:
  input =::
  off to < if{
    off to + 2 / int c mut =:
    input cmp off c buffer mergesort2_helper
    c 1 + c =
    input cmp c  to buffer mergesort2_helper
    ## 0 ... c-1 and c...to-1 are sorted
    c int i mut =:
    while{ off c < &&{ c to <= } do
      while{ off c < &&{ input off [] input c [] cmp () 0 <= } do
        off 1 + off =
      }
      off c < if{
        while{ i to <= &&{ input off [] input i [] cmp () 0 > } do
          i 1 + i =
        }
        i c - int delta =:
        delta c off - <= if{
          buffer clear
          buffer input c i [:] <<* drop ## store data in buffer
          input off c [:]  input off delta + c delta + [:]= ## move remaining part of left half to the right
          buffer  input off  off delta + [:]= ## get the elements from buffer
          buffer clear
        else
          buffer clear
          buffer input off c [:] <<* drop ## store data in buffer
          input c i [:]  input off  off delta + [:]= ## move remaining part of left half to the right
          buffer  input off delta + i [:]= ## get the elements from buffer
          buffer clear
        }
        i c =
        off 1 + off =
      }
    }
  }
}

#+ sorts the given list
  elements are compared using the given comparison function
  the sorted list overwrites the given list
 +#
mergesort2 public proc( A <?> A mut list ( A A => int ) => ){
  ( A A => int ) cmp =:
  input =::
  #+list+# input #+cmp+# cmp #+off+# 0 #+to+# input .length 1 - #+buffer+# input .length 2 / A mut list new
  mergesort2_helper
}

map public proc( A <?> B <?> A mut? list ( A => B ) => B mut list ){
  f =::
  l =::
  l .length B mut list new res =::
  0 i =::
  res while{ i l .length < do
    l i [] f () <<
    i 1 + i =
  }
  #+res+# return
}
filter public proc( A <?> A mut? list ( A => bool ) => A mut list ){
  f =::
  l =::
  l .length A mut list new res =::
  0 i =::
  res while{ i l .length < do
    l i [] f () if{
      l i [] <<
    }
    i 1 + i =
  }
  #+res+# return
}

#end ## core