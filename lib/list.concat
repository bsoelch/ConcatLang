lib/list:

core #namespace
stack #include
codeblocks #include
arraySlice #include

## TODO better way of reallocating:
##   try moving offset to avoid reallocating
##   set growth rate to factor less than 2 ( 9/8 ? )

List public struct{ A <>
  A memory mut^ :data mut
}
ListSlice public struct{ A <>
  A List mut^ :parent
  uint        :offset
  uint        :length mut
}

newList public proc( A <> uint => A List mut ){
  A memory mut new A List mut new return
}
## wrappers for easier string handling
byte      List mut  type  stringBuilder public mut~ =:
codepoint List mut  type ustringBuilder public mut~ =:
buildString public proc( uint => stringBuilder ){
  byte newList return
}
buildString public proc( string mut? => stringBuilder ){
  str =::
  str .length byte newList str <<* return
}
buildUString public proc( uint => ustringBuilder ){
  codepoint newList return
}
build public proc( stringBuilder => string ){
  .data clone-mut~ return
}
build public proc( ustringBuilder => ustring ){
  .data clone-mut~ return
}

valueIO #include ## include valueIO after stringBuilder is declared

length public proc( A <?> A List mut? => uint ){
  #+list+# .data .length return
}

ensureCap public proc( A <?> A List mut uint => ){##addLater better names for capacity procedure, add procedure for offset
  newCap =::
  mList =::
  mList .data .capacity newCap < if{
    mList .data newCap realloc
  }
}
ensureCap public proc( A <?> A ListSlice mut uint => ){
  newCap =::
  slice =::
  slice .parent .data .capacity slice .parent .data .length slice .length - - newCap < if{
    slice .parent .data  slice .parent .data .length slice .length - newCap + realloc
  }
}

clear public proc( A <?> A List mut => ){
  mList =::
  mList .data 0 mList .data .length clearSlice
}
clear public proc( A <?> A ListSlice mut => ){
  slice =::
  slice .parent .data slice .offset slice .offset slice .length + clearSlice
  0 slice .length =
}
clearSlice public proc( A <?> A List mut uint uint => ){
  to =::
  off =::
  #+list+# .data off to clearSlice
}
clearSlice public proc( A <?> A ListSlice mut uint uint => ){
  to =::
  off =::
  slice =::
  off 0 < ||{ to off < ||{ to slice .length > } } if{
    64 buildString
    "invalid slice " <<* off <<i ":" <<* to <<u " length:" <<* slice .length <<u '\n' << build eputs
    1 exit
  }
  slice .parent .data slice .offset off + slice .offset to + clearSlice
  slice .length to off - - slice .length =
}

fill public proc( A <?> A A List mut int uint => ){
  count =::
  off =::
  ## cast to array to natively check bounds
  .data A array mut cast off count fill
}
fill public proc( A <?> A A ListSlice mut int uint => ){
  count =::
  off =::
  slice =::
  off 0 < ||{ off count + slice .length > } if{
    64 buildString
    "invalid slice " <<* off <<i ":" <<* count <<u " length:" <<* slice .length <<u '\n' << build eputs
    1 exit
  }
  #+val+# slice .parent slice .offset off + count fill
}

[] public proc( A <?> A List mut? uint => A ){
  i =::
  mList =::
  mList .data i [] return
}
[] public proc( A <?> A ListSlice mut? uint => A ){
  i =::
  slice =::
  i 0 < ||{ i slice .length >= } if{
    64 buildString
    "index out of bounds " <<* i <<i " length:" <<* slice .length <<u '\n' << build eputs
    1 exit
  }
  slice .parent i slice .offset + [] return
}

[]= public proc( A <?> A A List mut uint => ){
  i =::
  mList =::
  #+val+# mList .data i []=
}
[]= public proc( A <?> A A ListSlice mut uint => ){
  i =::
  slice =::
  i 0 < ||{ i slice .length >= } if{
    64 buildString
    "index out of bounds " <<* i <<i " length:" <<* slice .length <<u '\n' << build eputs
    1 exit
  }
  #+val+# slice .parent .data slice .offset i + []=
}

##inserts element after the given index
insert public proc( A <?> A A List mut uint => ){
  index =::
  mList =::
  mList .data listData =::
  index 0 < ||{ index listData .length > } if{
    64 buildString
    "index out of bounds " <<* index <<i " length:" <<* listData .length <<u '\n' << build eputs
    1 exit
  }
  listData .length listData .capacity >= if{
    listData .offset listData .capacity + 8 + delta =::
    listData listData .offset listData .capacity + delta + realloc
  }
  index listData .length < if{
    listData index listData index 1 + listData .length index - copy
    #+val+# listData index []=
  else
    #+val+# listData swap []^=
  }
}
insert public proc( A <?> A A ListSlice mut uint => ){
  index =::
  slice =::
  index 0 < ||{ index slice .length > } if{
    64 buildString
    "index out of bounds " <<* index <<i " length:" <<* slice .length <<u '\n' << build eputs
    1 exit
  }
  #+val+# slice .parent slice .offset index + insert
  slice .length 1 + slice .length =
}
insertAll public proc( A <?> A List mut? A List mut uint => ){
  index =::
  mList =::
  #+toInsert+# .data mList index insertAll
}
insertAll public proc( A <?> A array mut? A List mut uint => ){
  index =::
  mList =::
  mList .data listData =::
  toInsert =::
  index 0 < ||{ index listData .length > } if{
    64 buildString
    "index out of bounds " <<* index <<i " length:" <<* listData .length <<u '\n' << build eputs
    1 exit
  }
  listData .length toInsert .length + listData .capacity > if{
    toInsert .length delta =::
    listData listData .offset listData .capacity + delta + realloc
  }
  toInsert 0 listData index index toInsert .length copyToSlice
}
insertAll public proc( A <?> A ListSlice mut? A List mut uint => ){
  index =::
  mList =::
  mList .data listData =::
  toInsert =::
  index 0 < ||{ index listData .length > } if{
    64 buildString
    "index out of bounds " <<* index <<i " length:" <<* listData .length <<u '\n' << build eputs
    1 exit
  }
  listData .length toInsert .length + listData .capacity > if{
    toInsert .length delta =::
    listData listData .offset listData .capacity + delta + realloc
  }
  toInsert .parent .data toInsert .offset listData index index toInsert .length copyToSlice
}
insertAll public proc( A <?> A List mut? A ListSlice mut uint => ){
  index =::
  slice =::
  #+toInsert+# .data slice index insertAll
}
insertAll public proc( A <?> A array mut? A ListSlice mut uint => ){
  index =::
  slice =::
  toInsert =::
  index 0 < ||{ index slice .length > } if{
    64 buildString
    "index out of bounds " <<* index <<i " length:" <<* slice .length <<u '\n' << build eputs
    1 exit
  }
  slice .length toInsert .length + ##newLength
  toInsert slice .parent slice .offset index + insertAll
  #+newLength+# slice .length =
}
insertAll public proc( A <?> A ListSlice mut? A ListSlice mut uint => ){
  index =::
  slice =::
  toInsert =::
  index 0 < ||{ index slice .length > } if{
    64 buildString
    "index out of bounds " <<* index <<i " length:" <<* slice .length <<u '\n' << build eputs
    1 exit
  }
  slice .length toInsert .length + ##newLength
  toInsert slice .parent slice .offset index + insertAll
  #+newLength+# slice .length =
}

remove public proc( A <?> A List mut uint => A ){
  index =::
  #+mList+# .data listData =::
  listData index [] ## prev
  listData index index 1 + clearSlice
  #+prev+# return
}
remove public proc( A <?> A ListSlice mut uint => A ){
  index =::
  slice =::
  index 0 < ||{ index slice .length >= } if{
    64 buildString
    "index out of bounds " <<* index <<i " length:" <<* slice .length <<u '\n' << build eputs
    1 exit
  }
  index slice .offset + index =
  slice .parent .data index [] ## prev
  slice .parent .data index index 1 + clearSlice
  slice .length 1 - slice .length =
  #+prev+# return
}

getFirst public proc( A <?> A List mut? => A ){
   #+mList+# 0 [] return
}
getFirst public proc( A <?> A ListSlice mut? => A ){
   #+slice+# 0 [] return
}
getLast public proc( A <?> A List mut? => A ){
   mList =::
   mList mList .data .length 1 - [] return
}
getLast public proc( A <?> A ListSlice mut? => A ){
   slice =::
   slice slice .length 1 - [] return
}

peekFirst public proc( A <?> A List mut? => A optional ){
   mList =::
   mList .data .length 0 == if{
     A empty return
   }
   mList 0 [] wrap return
}
peekFirst public proc( A <?> A ListSlice mut? => A optional ){
   slice =::
   slice .length 0 == if{
     A empty return
   }
   slice 0 [] wrap return
}
peekLast public proc( A <?> A List mut? => A optional ){
   mList =::
   mList .data .length 0 == if{
     A empty return
   }
   mList mList .data .length 1 - [] wrap return
}
peekLast public proc( A <?> A ListSlice mut? => A optional ){
   slice =::
   slice .length 0 == if{
     A empty return
   }
   slice slice .length 1 - [] wrap return
}

removeFirst public proc( A <?> A List mut => A ){
   #+mList+# 0 remove return
}
removeFirst public proc( A <?> A ListSlice mut => A ){
   #+slice+# 0 remove return
}
removeLast public proc( A <?> A List mut => A ){
   mList =::
   mList mList .data .length 1 - remove return
}
removeLast public proc( A <?> A ListSlice mut => A ){
   slice =::
   slice slice .length 1 - remove return
}

popFirst public proc( A <?> A List mut => A optional ){
   mList =::
   mList .data .length 0 == if{
     A empty return
   }
   mList 0 remove wrap return
}
popFirst public proc( A <?> A ListSlice mut => A optional ){
   slice =::
   slice .length 0 == if{
     A empty return
   }
   slice 0 remove wrap return
}
popLast public proc( A <?> A List mut => A optional ){
   mList =::
   mList .data .length 0 == if{
     A empty return
   }
   mList mList .data .length 1 - remove wrap return
}
popLast public proc( A <?> A ListSlice mut => A optional ){
   slice =::
   slice .length 0 == if{
     A empty return
   }
   slice slice .length 1 - remove wrap return
}

<< public proc( A <?> A List mut A => A List mut ){
  val =::
  mList =::
  mList .data listData =::
  listData .length listData .capacity >= if{
    listData .offset listData .capacity + 8 + delta =::
    listData listData .offset listData .capacity + delta + realloc
  }
  listData val []^=
  mList return
}
<< public proc( A <?> A ListSlice mut A => A ListSlice mut ){
  val =::
  slice =::
  val slice .parent slice .offset slice .length + insert
  slice .length 1 + slice .length =
  slice return
}
>> public proc( A <?> A A List mut => A List mut ){
  mList =::
  mList .data listData =::
  val =::
  listData .offset 0 == if{
    listData .offset listData .capacity + 8 + delta =::
    listData listData .offset listData .capacity + delta + realloc
    listData delta setOffset
  }
  val listData ^[]=
  mList return
}
>> public proc( A <?> A A ListSlice mut => A ListSlice mut ){
  slice =::
  val =::
  val slice .parent slice .offset insert
  slice .length 1 + slice .length =
  slice return
}
<<* public proc( A <?> A List mut A List mut? => A List mut ){
  toAppend =::
  mList =::
  mList toAppend .data <<*
}
<<* public proc( A <?> A List mut A array mut? => A List mut ){
  toAppend =::
  mList =::
  mList .data listData =::
  listData .length toAppend .length + listData .capacity > if{
    toAppend .length delta =::
    listData listData .offset listData .capacity + delta + realloc
  }
  toAppend 0 listData listData .length toAppend .length copy
  mList return
}
<<* public proc( A <?> A List mut A ListSlice mut? => A List mut ){
  toAppend =::
  mList =::
  mList .data listData =::
  listData .length toAppend .length + listData .capacity > if{
    toAppend .length delta =::
    listData listData .offset listData .capacity + delta + realloc
  }
  toAppend .parent .data toAppend .offset listData listData .length toAppend .length copy
  mList return
}
<<* public proc( A <?> A List mut A arraySlice mut? => A List mut ){
  toAppend =::
  mList =::
  mList .data listData =::
  listData .length toAppend .length + listData .capacity > if{
    toAppend .length delta =::
    listData listData .offset listData .capacity + delta + realloc
  }
  toAppend .parent toAppend .offset listData listData .length toAppend .length copy
  mList return
}
<<* public proc( A <?> A ListSlice mut A List mut? => A ListSlice mut ){
  toAppend =::
  slice =::
  slice .length toAppend .data .length + ## newLength
  toAppend .data slice .parent slice .offset insertAll
  #+newLength+# slice .length =
  slice return
}
<<* public proc( A <?> A ListSlice mut A array mut? => A ListSlice mut ){
  toAppend =::
  slice =::
  slice .length toAppend .length + ## newLength
  toAppend slice .parent slice .offset insertAll
  #+newLength+# slice .length =
  slice return
}
<<* public proc( A <?> A ListSlice mut A ListSlice mut? => A ListSlice mut ){
  toAppend =::
  slice =::
  slice .length toAppend .length + ## newLength
  toAppend slice .parent slice .offset insertAll
  #+newLength+# slice .length =
  slice return
}
*>> public proc( A <?> A List mut? A List mut => A List mut ){
  mList =::
  toAppend =::
  toAppend .data mList *>>
}
*>> public proc( A <?> A array mut? A List mut => A List mut ){
  mList =::
  mList .data listData =::
  toAppend =::
  listData .offset toAppend .length < if{
    toAppend .length delta =::
    listData listData .offset listData .capacity + delta + realloc
    listData listData .offset delta + setOffset
  }
  toAppend 0 listData toAppend .length int cast -_ toAppend .length copy
  mList return
}
*>> public proc( A <?> A ListSlice mut? A List mut => A List mut ){
  mList =::
  mList .data listData =::
  toAppend =::
  listData .offset toAppend .length < if{
    toAppend .length delta =::
    listData listData .offset listData .capacity + delta + realloc
    listData listData .offset delta + setOffset
  }
  toAppend .parent .data toAppend .offset listData toAppend .length int cast -_ toAppend .length copy
  mList return
}
*>> public proc( A <?> A List mut? A ListSlice mut => A ListSlice mut ){
  slice =::
  toAppend =::
  slice .length toAppend .data .length + ## newLength
  toAppend .data slice .parent slice .offset slice .length + insertAll
  #+newLength+# slice .length =
  slice return
}
*>> public proc( A <?> A array mut? A ListSlice mut => A ListSlice mut ){
  slice =::
  toAppend =::
  slice .length toAppend .length + ## newLength
  toAppend slice .parent slice .offset slice .length + insertAll
  #+newLength+# slice .length =
  slice return
}
*>> public proc( A <?> A ListSlice mut? A ListSlice mut => A ListSlice mut ){
  slice =::
  toAppend =::
  slice .length toAppend .length + ## newLength
  toAppend slice .parent slice .offset slice .length + insertAll
  #+newLength+# slice .length =
  slice return
}

[:] public proc( A <?> A List mut uint uint => A ListSlice mut ){
  to =::
  off =::
  mList =::
  off 0 < ||{ to off < ||{ to mList .data .length > } } if{
    64 buildString
    "invalid slice " <<* off <<i ":" <<* to <<u " length:" <<* mList .data .length <<u '\n' << build eputs
    1 exit
  }
  mList off to off - A ListSlice mut new return
}
[:] public proc( A <?> A ListSlice mut uint uint => A ListSlice mut ){
  to =::
  off =::
  slice =::
  off 0 < ||{ to off < ||{ to slice .length > } } if{
    64 buildString
    "invalid slice " <<* off <<i ":" <<* to <<u " length:" <<* slice .length <<u '\n' << build eputs
    1 exit
  }
  slice .parent slice .offset off + slice .offset to + [:] return
}
## addLater [:] for non-mutable List/ListSlice

[:]= public proc( A <?> A array mut? A List mut uint uint => ){
  to =::
  off =::
  mList =::
  mList .data listData =::
  toInsert =::
  off 0 < ||{ to off < ||{ to listData .length > } } if{
    64 buildString
    "invalid slice " <<* off <<i ":" <<* to <<u " length:" <<* listData .length <<u '\n' << build eputs
    1 exit
  }
  listData .length toInsert .length to off - - + listData .capacity > if{
    toInsert .length to off - - delta =::
    listData listData .offset listData .capacity + delta + realloc
  }
  toInsert 0 listData off to toInsert .length copyToSlice return
}
[:]= public proc( A <?> A List mut? A List mut uint uint => ){
  to =::
  off =::
  mList =::
  #+toInsert+# .data mList off to [:]=
}
[:]= public proc( A <?> A ListSlice mut? A List mut uint uint => ){
  to =::
  off =::
  mList =::
  mList .data listData =::
  toInsert =::
  off 0 < ||{ to off < ||{ to listData .length > } } if{
    64 buildString
    "invalid slice " <<* off <<i ":" <<* to <<u " length:" <<* listData .length <<u '\n' << build eputs
    1 exit
  }
  listData .length toInsert .length to off - - + listData .capacity > if{
    toInsert .length to off - - delta =::
    listData listData .offset listData .capacity + delta + realloc
  }
  toInsert .parent .data toInsert .offset listData off to toInsert .length copyToSlice return
}
[:]= public proc( A <?> A array mut? A ListSlice mut uint uint => ){
  to =::
  off =::
  slice =::
  toInsert =::
  off 0 < ||{ to off < ||{ to slice .length > } } if{
    64 buildString
    "invalid slice " <<* off <<i ":" <<* to <<u " length:" <<* slice .length <<u '\n' << build eputs
    1 exit
  }
  slice .length toInsert .length to off - - + ## newLength
  toInsert slice .parent slice .offset off + slice .offset to + [:]=
  #+newLength+# slice .length =
}
[:]= public proc( A <?> A List mut? A ListSlice mut uint uint => ){
  to =::
  off =::
  slice =::
  toInsert =::
  slice .length toInsert .data .length to off - - + ## newLength
  toInsert .data slice off to [:]=
  #+newLength+# slice .length =
}
[:]= public proc( A <?> A ListSlice mut? A ListSlice mut uint uint => ){
  to =::
  off =::
  slice =::
  toInsert =::
  off 0 < ||{ to off < ||{ to slice .length > } } if{
    64 buildString
    "invalid slice " <<* off <<i ":" <<* to <<u " length:" <<* slice .length <<u '\n' << build eputs
    1 exit
  }
  slice .length toInsert .length to off - - + ## newLength
  toInsert slice .parent slice .offset off + slice .offset to + [:]=
  #+newLength+# slice .length =
}

#end ## core