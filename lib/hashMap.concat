lib/hashMap :

hashMap #namespace

stack #include
codeblocks #include
core #import

listHash public proc( A <?> A list mut? => uint ){
  A list mut? value =:
  0 uint hash mut =:
  0 uint i mut =:
  while{ i value .length < do
    hash 31 * value i [] hashCode + hash =
    i 1 + i =
  }
  hash return
}
arrayHash public proc( A <?> A array mut? => uint ){
  A array mut? value =:
  0 uint hash mut =:
  0 uint i mut =:
  while{ i value .length < do
    hash 31 * value i [] hashCode + hash =
    i 1 + i =
  }
  hash return
}
hashCode public proc( var => uint ){
  var value =:
  value .type type valType =:
  ## addLater? use array-hash for tuple/enum-types
  valType .isList if{
    value listHash return
  else valType .isArray ||{ valType .isMemory } if
    value arrayHash return
  else valType ( ) <= if
    0 uint hash mut =:
    0 uint i mut =:
    while{ i value ( ) cast .length < do
      hash 31 * value ( ) cast i [] hashCode + hash =
      i 1 + i =
    }
    hash
  else
    value refId
  }
}

MapEntry public struct{ K <> V <>
  K :key mut
  V :value mut
}

HashMapEntry struct{ K <> V <> K V MapEntry extend
  K V MapEntry mut optional :next mut ## use MapEntry instead of HashMapEntry because self-containing structs are not supported
}

HashMap public struct{ K <> V <>
  K V HashMapEntry mut optional array mut :data  ## addLater? (automatically) adjust capacity if size gets to large
  int :size mut
}
HashMap.incSize proc( K <?> V <?> K V HashMap mut => ){ dup .size 1 + swap .size = }
HashMap.decSize proc( K <?> V <?> K V HashMap mut => ){ dup .size 1 - swap .size = }

newHashMap public proc( #+keyType+# K <> #+valType+# V <> #+cap+# int => K V HashMap mut ){
  int cap =:
  K V HashMapEntry mut empty  cap  K V HashMapEntry mut optional array mut new data =::
  data 0 K V HashMap mut new
}

HashMap.put public proc( K <?> V <?> #+value+# V #+map+# K V HashMap mut #+key+# K => #+prevVal+# V optional ){
  K key =:
  K V HashMap mut map =:
  V value =:
  key hashCode uint hash =:
  hash map .data .length % uint i =:
  map .data i [] if{
    K V HashMapEntry mut entry mut =:
    while{ entry .next do
      entry .key key == if{ drop ##drop next
        ##replace does not change size
        entry .value
        value entry .value =
        wrap return
      }
      entry =
    } ##drop last entry
    entry .key key == if{
      ##replace does not change size
      entry .value
      value entry .value =
      wrap return
    else
      key value K V HashMapEntry mut empty K V HashMapEntry mut new wrap  entry .next =
      map HashMap.incSize
      V empty return
    }
  else
    key value K V HashMapEntry mut empty K V HashMapEntry mut new wrap   map .data i []=
    map HashMap.incSize
    V empty return
  }
}

[]= public proc( K <?> V <?> #+value+# V #+map+# K V HashMap mut #+key+# K => #+prevVal+# V optional ){
  HashMap.put return
}

HashMap.contains public proc( K <?> V <?> #+map+# K V HashMap mut? #+key+# K => bool ){
  K key =:
  K V HashMap mut? map =:
  key hashCode uint hash =:
  hash map .data .length % uint i =:
  map .data i [] K V HashMapEntry mut optional entry mut =:
  while{ entry do
    K V HashMapEntry mut unwrapped =:
    unwrapped .key key == if{
      true return
    }
    unwrapped .next entry =
  }
  false return
}

HashMap.get public proc( K <?> V <?> #+map+# K V HashMap mut? #+key+# K => #+value+# V optional ){
  K key =:
  K V HashMap mut? map =:
  key hashCode uint hash =:
  hash map .data .length % uint i =:
  map .data i [] K V HashMapEntry mut optional entry mut =:
  while{ entry do
    K V HashMapEntry mut unwrapped =:
    unwrapped .key key == if{
      unwrapped .value wrap return
    }
    unwrapped .next entry =
  }
  V empty return
}
[] public proc( K <?> V <?> #+map+# K V HashMap mut? #+key+# K => #+value+# V optional ){
  HashMap.get return
}

HashMap.remove public proc( K <?> V <?> #+map+# K V HashMap mut #+key+# K => #+prevVal+# V optional ){
  K key =:
  K V HashMap mut map =:
  key hashCode uint hash =:
  hash map .data .length % uint i =:
  map .data i [] if{
    K V HashMapEntry mut entry mut =:
    entry .key key == if{
      map HashMap.decSize
      entry .value ## get previous value
      entry .next K V HashMapEntry mut optional cast  map .data i []= ## remove entry
      wrap return
    else
      while{ entry .next do
        K V HashMapEntry mut next =:
        next .key key == if{
          map HashMap.decSize
          next .value ## get previous value
          next .next  entry .next = ## remove entry
          wrap return
        }
        next entry =
      }
      V empty return
    }
  else
    V empty return
  }
}

HashMapItr public struct{ K <> V <>
  K V HashMap mut?              :map
  int                           :index mut
  K V HashMapEntry mut optional :next  mut
}
HashMapItr.incI  proc( K <?> V <?> K V HashMapItr mut => ){ dup .index 1 + swap .index = }

^_ public proc( K <?> V <?> K V HashMap mut? => K V HashMapItr mut ){
  #+map+# -1 K V HashMapEntry mut empty K V HashMapItr mut new return
}
^> public proc( K <?> V <?> K V HashMapItr mut => K V HashMapItr mut K V MapEntry mut? optional ){
  K V HashMapItr mut itr =:
  while{ itr .next ! do
    itr HashMapItr.incI
    itr .index itr .map .data .length >= if{
      itr K V MapEntry empty return
    }
    itr .map .data itr .index []  itr .next =
  }
  itr .next K V MapEntry mut optional res =:
  itr .next if{
    .next itr .next =
  else unreachable }
  itr res return
}

## addLater? HashMap.listEntries
## addLater? HashMap.toString


#end ## hashMap